% PT Scanner.ssl   Ver. 1.03
% Abstract
%     The scanner breaks a PT source program up into syntax tokens.
%   It removes token separators - blanks, tabs, comments.  The
%   scanner detects these lexical errors:  unclosed comments,
%   unclosed literals, literals that cross a line boundary,
%   illegal characters and integers followed by identifiers with no
%   separator between them.

Input :
	lLetter 
	firstCharClass = lLetter 
	lDigit
	lPlus		'+'
	lMinus		'-'
	lStar		'*'
	lColon		':'
	lEquals		'='
	lDot		'.'
	lComma		','
	lSemicolon	';'
%%%%%%%%%%% ADDED 
	lSlash		'/'
	lHash		'#'
	lExclamation	'!'
	lAtSign		'@'
%%%%%%%%%%% 
	lQuote
	lLeftAngle	'<'
	lRightAngle	'>'
	lLeftParen	'('
	lRightParen	')'
	lLeftBracket	'['
	lRightBracket	']'
	lLeftBrace	'{'
	lRightBrace	'}'
	lBlank
	lIllegal
	lNewLine
	lTab
	lNewPage 
	lEndFile
	lastCharClass = lEndFile;

Output :
	% Keyword tokens -
	%   Tokens pDiv through pRepeat are keywords which will be separated
	%   from user defined identifiers by the Screener.  This Scanner
	%   treats them all as identifiers.
	pDiv 
	firstKeywordToken = pDiv
	pMod
	pOr
	pAnd
	pNot
%%%%%%%%%%% DELETED
%	pThen
%%%%%%%%%%% 
	pElse
	pOf
%%%%%%%%%%% DELETED
%	pEnd
%	pUntil
%	pDo
%%%%%%%%%%% 
	pArray
	pFile
	pPacked
	pProgram
%%%%%%%%%%% CHANGED
%	pConst
%%%%%%%%%%% 
	pLet
%%%%%%%%%%% 
	pVar
	pType
%%%%%%%%%%% CHANGED
%	pProcedure
%%%%%%%%%%% 
	pFunction
%%%%%%%%%%% 
%%%%%%%%%%% DELETED
%	pBegin
%%%%%%%%%%%
	pIf
	pCase
%%%%%%%%%%% ADDED
	pSwitch
	pDefault
%%%%%%%%%%%
%%%%%%%%%%% CHANGED
%	pWhile
%	pRepeat
%%%%%%%%%%% 
	pElsif
	pLoop
	pBreak
	pWhen
	pClass
	pPublic
%%%%%%%%%%% 
%%%%%%%%%%% ADDED 858
	pReturn
%%%%%%%%%%% 
%%%%%%%%%%% CHANGED 458
%	lastKeywordToken = pRepeat
%%%%%%%%%%% CHANGED 858
%	lastKeywordToken = pPublic
%%%%%%%%%%% 
	lastKeywordToken = pReturn
%%%%%%%%%%% 

	% Scanner tokens -
	%   These are the tokens actually recognized by the Scanner itself.

	% Compound tokens - 
	%   These will be sent to the parser with a value or index.
	pIdentifier 
	firstCompoundInputToken = pIdentifier
	pInteger
	pLiteral
	lastCompoundInputToken = pLiteral

	% Non-compound tokens - 
	%   These tokens simply represent themselves.
	pNewLine
	pEndFile
	pPlus
	pMinus
	pStar
%%%%%%%%%%% DELETED
%	pColonEquals
%%%%%%%%%%% 
	pDot
	pComma
	pSemicolon
%%%%%%%%%%% ADDED
	pHash
	pAtSign
	pEqualsEquals
%%%%%%%%%%% 
	pColon
	pEquals
	pNotEqual
	pLess
	pLessEqual
	pGreaterEqual
	pGreater
	pLeftParen
	pRightParen
	pLeftBracket
	pRightBracket
%%%%%%%%%%% ADDED
	pLeftBrace
	pRightBrace
%%%%%%%%%%% 
	pDotDot
	lastSyntaxToken = pDotDot;

Error :
	eIllegalChar = 5
	firstScanError = eIllegalChar
	eLiteralLine
	eLiteralEOF
	eCommentEOF
	eNoSeparator
	lastScanError = eNoSeparator;

mechanism Buffer :
    % Buffer saves the characters associated with the
    % compound tokens identifier, integer and literal.

    oBufferSave;
	% Save the character form of the last accepted lexical token.




rules

% Note that the order of choice alternatives
% may have a significant effect on performance
% under some implementations of S/SL.
Scan :
	{[
	    | lBlank :
	    | lLetter:
		oBufferSave
		@Identifier
	    | lNewLine:
		.pNewLine
	    | lTab:
	    | ';':
		.pSemicolon
	    | '(':
%%%%%%%%%%% CHANGED
%		[
%		    | '*':
%			@AlternateComment
%		    | *:
%			.pLeftParen
%		]
%%%%%%%%%%% 
		.pLeftParen
%%%%%%%%%%% 
	    | ')':
		.pRightParen
	    | '[':
		.pLeftBracket
	    | ']':
		.pRightBracket
	    | ',':
		.pComma
	    | ':':
%%%%%%%%%%% CHANGED 
%		[
%		    | '=':
%			.pColonEquals
%		    | *:
%			.pColon
%		]
%%%%%%%%%%% 
		.pColon
%%%%%%%%%%% 
	    | lDigit:
		oBufferSave
		@Integer
		[
		    | lLetter:
			#eNoSeparator
			oBufferSave
			@Identifier
		    | *:
		]
	    | '=':
%%%%%%%%%%% CHANGED 
%		.pEquals
%%%%%%%%%%% 
		[
		    | '=':
			.pEqualsEquals
		    | *:
			.pEquals
		]
%%%%%%%%%%% 
	    | '+':
		.pPlus
	    | lQuote:
		@Literal
	    | '-':
		.pMinus
	    | '.':
		[
		    | '.':
			.pDotDot
		    | *:
			.pDot
		]
	    | '<':
		[
%%%%%%%%%%% DELETED 
%		    | '>':
%			.pNotEqual
%%%%%%%%%%% 
		    | '=':
			.pLessEqual
		    | *:
			.pLess
		]
	    | '>':
		[
		    | '=':
			.pGreaterEqual
		    | *:
			.pGreater
		]
%%%%%%%%%%% CHANGED
%	    | '{':
%		@Comment
%%%%%%%%%%% 
	    | '/':
		[
		    | '/':
			@Comment
		    | '*':
			@AlternateComment
		    | *:
			#eIllegalChar
		]
%%%%%%%%%%% 
	    | '*':
		.pStar
%%%%%%%%%%% ADDED 
	    | '#':
		.pHash
	    | '@':
		.pAtSign
	    | '!':
		[
		    | '=':
			.pNotEqual
		    | *:
			#eIllegalChar
		]
	    | lNewPage:
%%%%%%%%%%% CHANGED
%	    | lIllegal, '}':
%		#eIllegalChar
%%%%%%%%%%% 
	    | '{':
	    	.pLeftBrace
	    | '}':
	    	.pRightBrace
	    | lIllegal :
		#eIllegalChar
%%%%%%%%%%% 
	    | lEndFile:
		.pEndFile
	]};

Identifier :
	% Assemble an identifier token

	{[
	    | lLetter, lDigit:
		oBufferSave
	    | *:
		.pIdentifier
		>
	]};

Integer :
	% Assemble an integer token

	{[
	    | lDigit:
		oBufferSave
	    | *:
		.pInteger
		>
	]};

Literal :
	% Assemble a literal string token

	{[
	    | lQuote:
		[
		    | lQuote:
			oBufferSave
		    | *:
			.pLiteral
			>
		]
	    | lNewLine:		% literal must not cross a line boundary
		#eLiteralLine
		.pNewLine
		>
	    | lEndFile:		% literal must be closed before EOF
		#eLiteralEOF
		.pEndFile
		>
	    | *:	% literal may contain any other character
		?
		oBufferSave
	]};

Comment :
	% Discard the contents of a comment

	{[
%%%%%%%%%%% DELETED
%	    | '}':
%		>
%%%%%%%%%%%
	    | lNewLine:
		.pNewLine
%%%%%%%%%%% ADDED
		>
%%%%%%%%%%%
	    | lEndFile:		% comment must be closed before EOF
		#eCommentEOF
		.pEndFile
		>
	    | *:	% comment may contain any other character
		?
	]};

AlternateComment :
	{[
	    | '*':
		[
%%%%%%%%%%% CHANGED
%		    | ')':
%%%%%%%%%%% 
		    | '/':
%%%%%%%%%%% 
			>
		    | *:
		]
	    | lNewLine:
		.pNewLine
	    | lEndFile:
		#eCommentEOF
		.pEndFile
		>
	    | *:
		?
	]};

end
